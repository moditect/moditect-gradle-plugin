plugins {
    id "java"
    id "application"
    id "org.moditect.gradleplugin" version "1.0.0-SNAPSHOT"
}

repositories {
    jcenter()
}

group = "org.moditect"
version = "2.0.0"

targetCompatibility = JavaVersion.VERSION_1_9
sourceCompatibility = JavaVersion.VERSION_1_9

ext.moduleName = 'com.example'

mainClassName = 'com.example.ValidationTest'
jar {
    manifest {
        attributes("Automatic-Module-Name": 'com.example')
    }
}

def currentOS = org.gradle.internal.os.OperatingSystem.current()
ext.platform = currentOS.windows ? 'win' : currentOS.linux ? 'linux' : 'mac'

dependencies{
    implementation 'org.hibernate.validator:hibernate-validator:6.0.13.Final'
    implementation 'org.glassfish:javax.el:3.0.1-b08'
    implementation "org.openjfx:javafx-base:11:$platform"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

moditect {
    addMainModuleInfo {
        version = project.version
        overwriteExistingFiles = false
        jdepsExtraArgs = ['-q']
        module {
            mainClass = mainClassName
            moduleInfo {
                name = moduleName
                open = true
            }
        }
    }
    addDependenciesModuleInfo {
        jdepsExtraArgs = ['-q']
        modules {
            module {
                artifact 'javax.validation:validation-api:2.0+'
                moduleInfo {
                    addServiceUses = true
                }
            }
            module {
                artifact 'org.glassfish:javax.el:3.0+'
                moduleInfo {
                    name = "java.el"
                }
            }
            module {
                artifact('com.fasterxml:classmate:1.3.4') {
                    exclude module: 'joda-time'  // Not needed; just for test
                }
                moduleInfo {
                    name = "com.fasterxml.classmate"
                }
            }
            module {
                artifact 'org.hibernate.validator:hibernate-validator'
                moduleInfo {
                    name = "org.hibernate.validator"
                    requires = '''
                        static jboss.logging.annotations;
                        static javafx.base;
                        transitive java.validation;
                        *;
                    '''
                    exports = '''
                        org.hibernate.validator.internal.util.logging to org.jboss.logging;
                        !org.hibernate.validator.internal*;
                        *;
                    '''
                    uses = '''
                        javax.validation.ConstraintValidator;
                        javax.validation.valueextraction.ValueExtractor;
                    '''
                }
            }
            module {
                artifact group: "org.jboss.logging", name: "jboss-logging", version: '3.3.2.Final'
                moduleInfo {
                    name = "org.jboss.logging"
                    requires = '''
                        static jboss.logmanager;
                        static log4j;
                        static log4j.api;
                        static slf4j.api;
                        *;
                    '''
                }
            }
            module {
                artifact 'org.jboss.logging:jboss-logging-processor:2.0.2.Final'
                moduleInfo {
                    name = "org.jboss.logging.processor"
                    requires = '''
                        static org.jboss.logging;
                        static log4j;
                        static log4j.api;
                        static slf4j.api;
                        *;
                    '''
                }
            }
        }
    }
    createRuntimeImage {
        modulePath = [file("$buildDir/modules")]
        modules = ['com.example', 'org.hibernate.validator']
        launcher {
            name = 'validationTest'
            module = 'com.example'
        }
        compression = 2
        stripDebug = true
    }
}
